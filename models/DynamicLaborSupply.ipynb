{
  "cells": [
    {
      "cell_type": "raw",
      "metadata": {},
      "source": [
        "---\n",
        "title: \"A Simple Labor Supply Model\"\n",
        "---"
      ],
      "id": "a466d1dd"
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## Model Setup and Solution\n",
        "\n",
        "Consider a dynamic labor supply model (with no uncertainty) where each agent $n$ chooses a sequence of consumption and hours, $\\{c_{t},h_{t}\\}_{t=1}^{\\infty}$, to solve:\n",
        "$$ \\max \\sum_{t=0}^\\infty \\beta^{t} \\left(\\frac{c_{t}^{1-\\sigma}}{1-\\sigma} - \\frac{\\alpha_{n}^{-1}}{1 + 1/\\psi}h_{t}^{1+1/\\psi}\\right)$$\n",
        "subject to the intertemporal budget constraint:\n",
        "$$ \\sum_{t}q_{t}c_{t} \\leq A_{n,0} + \\sum_{t}q_{t}W_{n,t}h_{t},\\qquad q_{t} = (1+r)^{-t}.$$\n",
        "Let $H_{n,t}$ and $C_{n,t}$ be the realizations of labor supply for agent $n$ at time $t$. Labor supply in this model obeys:\n",
        "$$H_{n,t}^{1/\\psi} = (\\alpha_{n}W_{n,t})C^{-\\sigma}_{n,t}.$$\n",
        "To simplify below, assume that $\\beta=(1+r)^{-1}$, so that the optimal solution features perfectly smoothed consumption, $C^*_{n}$. Making appropriate substitutions gives $C^*_{n}$ as the solution to:\n",
        "$$ \\left(\\sum_{t}q_{t}\\right)C^*_{n} = \\sum_{t}\\left(q_{t}W_{n,t}^{1+\\psi}\\right)\\alpha_{n}^{\\psi}(C_{n}^*)^{-\\psi\\sigma} + A_{n,0}.$$\n",
        "\n",
        "## Code to solve the model\n",
        "\n",
        "There is only one object to solve here which is consumption given a sequence of net wages. If one were to assume also constant wages (or net ages in the case of [Assignment 2](../assignments/Assignment-2.qmd), the function below solves optimal consumption.\n"
      ],
      "id": "be631307"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using Optim\n",
        "function solve_consumption(r,α,W,A,σ,ψ)\n",
        "    Q = 1/ (1 - 1/(1+r))\n",
        "    f(c) = (Q * c - Q * W^(1 + ψ) * α^ψ * c^(-σ*ψ) - A)^2\n",
        "    r = Optim.optimize(f,0.,A+W)\n",
        "    return r.minimizer\n",
        "end"
      ],
      "id": "8576e244",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## Code to simulate a cross-section\n",
        "\n",
        "Here we'll assume that wages, tastes for work, and assets co-vary systematically. For simplicity we'll use a multivariate log-normal distribution.\n",
        "\n",
        "Below is code to simulate a cross-section of 1,000 observations."
      ],
      "id": "2dc8f09b"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using Distributions\n",
        "function simulate_data(σ,ψ,r,N)\n",
        "    ch = [0.3 0. 0.; 0.5 0.5 0.; 0.4 0.8 1.8]\n",
        "    Σ = ch * ch'\n",
        "    X = rand(MvNormal(Σ),N)\n",
        "    α = exp.(X[1,:])\n",
        "    W = exp.(X[2,:])\n",
        "    A = exp.(X[3,:])\n",
        "    C = [solve_consumption(r,α[i],W[i],A[i],σ,ψ) for i in eachindex(A)]\n",
        "    @views H = exp.( X[1,:] .+ ψ .* X[2,:] .- ψ * σ .* log.(C) )\n",
        "    return (;α,W,A,C,H)\n",
        "end\n",
        "\n",
        "# assume risk-aversion of 2 and frisch of 0.5\n",
        "σ = 2.\n",
        "ψ = 0.5\n",
        "r = 0.05\n",
        "\n",
        "dat = simulate_data(σ,ψ,r,1_000)\n"
      ],
      "id": "22e82929",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## Simulating bias in OLS\n",
        "\n",
        "We know that OLS will be biased here, but let's run a simulation anyway to hammer the point home.\n"
      ],
      "id": "535c593c"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "function simulate_bias(B,N,σ,ψ,r)\n",
        "    ψ_est = zeros(B)\n",
        "    σ_est = zeros(B)\n",
        "    for b in 1:B\n",
        "        dat = simulate_data(σ,ψ,r,N)\n",
        "        X = [ones(N) log.(dat.W) log.(dat.C)]\n",
        "        Y = log.(dat.H)\n",
        "        b_est = inv(X' * X) * X' * Y\n",
        "        ψ_est[b] = b_est[2]\n",
        "        σ_est[b] = -b_est[3] / b_est[2]\n",
        "    end\n",
        "    return ψ_est,σ_est\n",
        "end\n",
        "\n",
        "ψ_est,σ_est = simulate_bias(500,1_000,σ,ψ,r)"
      ],
      "id": "732c5a1d",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "Given the covariance structure, we can see a clear bias in the estimates.\n"
      ],
      "id": "11c714e7"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "using Plots\n",
        "histogram(ψ_est)"
      ],
      "id": "16dc6f42",
      "execution_count": null,
      "outputs": []
    }
  ],
  "metadata": {
    "kernelspec": {
      "name": "julia-1.0",
      "language": "julia",
      "display_name": "Julia 1.0.0"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 5
}